apply plugin: 'java'
apply plugin: 'application'

repositories {
    jcenter()
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    testCompile 'junit:junit:4.12'
    integrationTestCompile 'org.testfx:testfx-core:4.0.10-alpha'
    integrationTestCompile 'org.testfx:testfx-junit:4.0.10-alpha'
}

mainClassName = 'console.TicTacToeApplication'
run.standardInput = System.in

task integrationTest(type: Test, group: 'verification', description: 'Runs the integration tests.') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}
check.dependsOn integrationTest

tasks.withType(Test) {
    reports.html.destination = file("${reportsDir}/${name}")
    reports.junitXml.destination = file("${testResultsDir}/${name}")
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

def workaroundToFixIdeaIdeNotDetectingIntegrationTestSourceSets = {
    apply plugin: 'idea'
    idea {
        module {
            testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
            testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        }
    }
}()
